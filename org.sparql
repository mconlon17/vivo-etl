PREFIX my: <http://my.org/#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX iao: <http://purl.obolibrary.org/obo/iao.owl#IAO_>
PREFIX org: <http://purl.obolibrary.org/obo/org.owl#ORG_>
PREFIX ro: <http://purl.obolibrary.org/obo/ro.org#RO_>
PREFIX vivo: <http://purl.obolibrary.org/obo/vivo.owl#VIVO_>


CONSTRUCT {
    ?org_uri a org:0000001 .                # create an org       
    ?org_uri rdfs:label ?name .             # name the org
    
    ?org_uri vivo:0002001 ?acronym .        # has abbreviation
    
    ?org_uri org:0000002 ?status_uri .      # org has status
    ?org_uri ro:0000091 ?disposition_uri .  # org has disposition .
    
    ?org_uri vivo:0002000 ?country_uri .    # org has location in country
    
    ?org_uri iao:0000235 ?ror_uri .         # the org denotedBy an id
    ?ror_uri a iao:0022022 .                # the id is a ror (ido)
    ?ror_uri iao:0022342 ?ror .             # the ror has a value
    
    ?org_uri iao:0000235 ?grid_uri .        # the org denotedBy an id
    ?grid_uri a iao:0022010 .               # the id is a grid (ido)
    ?grid_uri iao:0022342 ?grid .           # the grid has a value
    
    ?org_uri iao:0000235 ?isni_uri .        # the org denotedBy an id
    ?isni_uri a iao:0022014 .               # the id is an isni (ido)
    ?isni_uri iao:0022342 ?isni .           # the isni has a value
            
    ?org_uri iao:0000235 ?wikidata_uri .    # the org denotedBy an id
    ?wikidata_uri a iao:0022027 .           # the id is an wikidata qid (ido)
    ?wikidata_uri iao:0022342 ?wikidata .   # the wikidata q number has a value
    
    ?org_uri iao:0000235 ?orgref_uri .      # the org denotedBy an id
    ?orgref_uri a iao:0022003 .             # the id is an orgref (ido)
    ?orgref_uri iao:0022342 ?orgref .       # the orgref q number has a value
    
    ?link_uri iao:0000136 ?org_uri .        # link is about org
    ?link_uri a iao:90012234 .              # link is a website
    ?link_uri iao:0022342 ?link .           # link has value

    }

WHERE {	
    
    # org_uri and ROR id
    
    ?s my:id ?ror .
    BIND(IRI(CONCAT("http://vivoweb.org/org/ror",strafter(str(?ror), "https://ror.org/"))) AS ?org_uri)
    BIND(IRI(CONCAT(str(?org_uri),"rorid")) AS ?ror_uri)

    ?s my:name ?name .
           
    # Acronyms (abbreviation)
    
    ?s my:acronyms ?acronym .
    
    # Status
    
    ?s my:status ?status .
    BIND( IF(?status = "active", org:0000003, org:0000004) AS ?status_uri)
    
    # Type (disposition)
    
    ?s my:types ?type .
    BIND( IF(?type = "Healthcare", 	org:0000005, 
          IF(?type = "Education", 	org:0000006,
          IF(?type = "Other", 		org:0000007,
          IF(?type = "Company", 	org:0000008,
          IF(?type = "Archive", 	org:0000009,
          IF(?type = "Nonprofit", 	org:0000010,
          IF(?type = "Government", 	org:0000011,
          IF(?type = "Facility", 	org:0000012,
          org:0000013)
    ))))))) AS ?disposition_uri)
          
    # Country
    
    ?s my:country ?c .
    ?c my:country_code ?c_code .
    BIND(IRI(CONCAT("http://vivoweb.org/org/country",?c_code)) AS ?country_uri)
    
    # Links (home page)
    
    ?s my:links ?link .
    BIND(IRI(CONCAT(str(?org_uri),"link")) AS ?link_uri) 
    
    # Grid ID
    
    ?s my:external_ids ?eids .
    ?eids my:GRID ?grid_id .
    ?grid_id my:all ?grid .
    BIND(IRI(CONCAT(str(?org_uri),"gridid")) AS ?grid_uri)
    
    # ISNI
    
    ?s my:external_ids ?eids .
    ?eids my:ISNI ?isni_id .
    ?isni_id my:all ?isni .
    BIND(IRI(CONCAT(str(?org_uri),"isniid")) AS ?isni_uri)
    
    # Wikidata Q number
    
    ?s my:external_ids ?eids .
    ?eids my:Wikidata ?wikidata_id .
    ?wikidata_id my:all ?wikidata .
    BIND(IRI(CONCAT(str(?org_uri),"wikidataqid")) AS ?wikidata_uri)
    
    # OrgRef number
    
    ?s my:external_ids ?eids .
    ?eids my:OrgRef ?orgref_id .
    ?orgref_id my:all ?orgref .
    BIND(IRI(CONCAT(str(?org_uri),"orgrefid")) AS ?orgref_uri)

}
