PREFIX my: <http://my.org#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>
PREFIX vivo: <http://vivoweb.org/ontology/core#>


CONSTRUCT {
    ?org_uri a foaf:Organization .           # create an org       
    ?org_uri rdfs:label ?name .              # name the org
    
    ?org_uri vivo:abbreviation ?acronym .    # has abbreviation
    
#   ?org_uri org:0000002 ?status_uri .       # VIVO cannot represent status

    ?org_uri a ?type_uri .                   # org has type
    
    ?org_uri vivo:0002000 ?country_uri .     # org has location in country
    
#   ?org_uri iao:0000235 ?ror_uri .          # VIVO cannot represent ROR
#   ?ror_uri a iao:0022022 .                 # the id is a ror (ido)
#   ?ror_uri iao:0022342 ?ror .              # the ror has a value
    
#   ?org_uri iao:0000235 ?grid_uri .         # VIVO cannot represent grid
#   ?grid_uri a iao:0022010 .                # the id is a grid (ido)
#   ?grid_uri iao:0022342 ?grid .            # the grid has a value
    
#   ?org_uri iao:0000235 ?isni_uri .         # VIVO cannot represent ISNI
#   ?isni_uri a iao:0022014 .                # the id is an isni (ido)
#   ?isni_uri iao:0022342 ?isni .            # the isni has a value
            
#   ?org_uri iao:0000235 ?wikidata_uri .     # VIVO cannot represent QID
#   ?wikidata_uri a iao:0022027 .            # the id is an wikidata qid (ido)
#   ?wikidata_uri iao:0022342 ?wikidata .    # the wikidata q number has a value
    
#   ?org_uri iao:0000235 ?orgref_uri .       # VIVO cannot represent orgref
#   ?orgref_uri a iao:0022003 .              # the id is an orgref (ido)
#   ?orgref_uri iao:0022342 ?orgref .        # the orgref q number has a value
    
    ?org_uri obo:ARG_2000028 ?vcard_uri .    # org has a vcard
    ?vcard_uri vcard:hasURL ?link_uri .      # vcard has a url
    ?link_uri vcard:url ?link .              # url has a link value
    ?link_uri rdfs:label "Homepage"@en .     # the url is a homepage

    }

WHERE {	
    
    # org_uri and ROR id
    
    ?s my:id ?ror .
    BIND(IRI(CONCAT("http://vivoweb.org/org/ror",strafter(str(?ror), "https://ror.org/"))) AS ?org_uri)
    BIND(IRI(CONCAT(str(?org_uri),"rorid")) AS ?ror_uri)

    ?s my:name ?name .
           
    # Acronyms (abbreviation)
    
    ?s my:acronyms ?acronym .
    
    # Status
    
#    ?s my:status ?status .
#    BIND( IF(?status = "active", org:0000003, org:0000004) AS ?status_uri)
    
    # Type (disposition)
    
    ?s my:types ?type .
    BIND( IF(?type = "Healthcare", 	vivo:Hospital, 
          IF(?type = "Education", 	vivo:University,
          IF(?type = "Company", 	vivo:Company,
          IF(?type = "Archive", 	vivo:Library,
          IF(?type = "Nonprofit", 	vivo:Company,
          IF(?type = "Government", 	vivo:GovernmentAgency, ""
    )))))) AS ?type_uri)
          
    # Country
    
    ?s my:country ?c .
    ?c my:country_code ?c_code .
    BIND(IRI(CONCAT("http://vivoweb.org/org/country",?c_code)) AS ?country_uri)
    
    # Links (home page)
    
    ?s my:links ?link .
    BIND(IRI(CONCAT(str(?org_uri),"vcard")) AS ?vcard_uri) 
    BIND(IRI(CONCAT(str(?org_uri),"homepage")) AS ?link_uri) 
    
    # Grid ID
    
    ?s my:external_ids ?eids .
    ?eids my:GRID ?grid_id .
    ?grid_id my:all ?grid .
    BIND(IRI(CONCAT(str(?org_uri),"gridid")) AS ?grid_uri)
    
    # ISNI
    
    ?s my:external_ids ?eids .
    ?eids my:ISNI ?isni_id .
    ?isni_id my:all ?isni .
    BIND(IRI(CONCAT(str(?org_uri),"isniid")) AS ?isni_uri)
    
    # Wikidata Q number
    
    ?s my:external_ids ?eids .
    ?eids my:Wikidata ?wikidata_id .
    ?wikidata_id my:all ?wikidata .
    BIND(IRI(CONCAT(str(?org_uri),"wikidataqid")) AS ?wikidata_uri)
    
    # OrgRef number
    
    ?s my:external_ids ?eids .
    ?eids my:OrgRef ?orgref_id .
    ?orgref_id my:all ?orgref .
    BIND(IRI(CONCAT(str(?org_uri),"orgrefid")) AS ?orgref_uri)

}
