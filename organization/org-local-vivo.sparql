PREFIX my: <http://my.org#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>
PREFIX vivo: <http://vivoweb.org/ontology/core#>


CONSTRUCT {  

    ?org_uri a ?type_uri .                   # org has type    
    
    ?org_uri rdfs:label ?name .              # org has a name    
    
    ?org_uri vivo:overview ?overview .       # org has overview    
    
    ?org_uri vivo:abbreviation ?acronym .    # has abbreviation    
    
    ?org_uri obo:BFO_0000050 ?parent_uri .   # org is part of parent org

    ?org_uri obo:ARG_2000028 ?vcard_uri .    # org has a vcard
    ?vcard_uri vcard:hasURL ?link_uri .      # vcard has a url
    ?link_uri a vcard:URL .                  # link_uri is a vcard URL
    ?link_uri vcard:url ?link .              # url has a link value
    ?link_uri rdfs:label "Homepage"@en .     # the url is a homepage

    }

WHERE {	
    
# org_uri and local id
    
    ?s my:ID ?id .
    BIND(IRI(CONCAT("http://vivoweb.org/org/local",?id)) AS ?org_uri)
    
#   Name

    ?s my:Name ?name .
           
#   Acronyms (abbreviation)
    
    OPTIONAL { ?s my:Abbreviation ?acronym . }
    
#   Type (disposition)
    
    ?s my:Disposition ?type .
    BIND( IF(?type = "University", 	            vivo:University, 
          IF(?type = "College", 	            vivo:College,
          IF(?type = "AcademicDepartment", 	    vivo:Department,
          IF(?type = "CoreLaboratory", 	        vivo:CoreLaboratory,
          IF(?type = "Library", 	            vivo:Library,
          IF(?type = "ResearchAdministration", 	vivo:ResearchOrganization,
          IF(?type = "SurveyResearch", 	        vivo:ResearchOrganization,
          IF(?type = "AnimalCare", 	            vivo:ResearchOrganization,
          IF(?type = "StartupIncubation", 	    vivo:ResearchOrganization,         
          IF(?type = "ClinicalResearch", 	    vivo:ResearchOrganization,
                                                foaf:Organization
    )))))))))) AS ?type_uri)
          
#   Links (home page)
    
    ?s my:Homepage ?link .
    BIND(IRI(CONCAT(str(?org_uri),"vcard")) AS ?vcard_uri) 
    BIND(IRI(CONCAT(str(?org_uri),"homepage")) AS ?link_uri) 
     
#   Parent
    
    OPTIONAL {
    ?s my:Parent ?parent_id
    BIND(IRI(CONCAT("http://vivoweb.org/org/local",?parent_id)) AS ?parent_uri)
    }
    
#   Overview
    
    OPTIONAL { ?s my:Overview ?overview . }

}
